#Program name: >=&nPr;uiss4<=
#Password: <no password>
0 -> I
Lbl 8
3 -> O
ClrText
Locate 4, 1, "≡≡&nPr;uissance 4≡≡"
Locate 4, 3, "Jouer à 2"
Locate 4, 4, "Jouer contre l'IA"
Locate 4, 5, "Reprendre dernière"
Locate 4, 6, "sauvegarde"
Lbl 9
Locate 3, 3, " "
Locate 3, 4, " "
Locate 3, 5, " "
Locate 3, O, "▶"
Do
	GetKey
LpWhile Ans != 37 And Ans != 28 And Ans != 31
Ans = 28 => O - 1 -> O
Ans = 37 => O + 1 -> O
O = 2 => 5 -> O
O = 6 => 3 -> O
If Ans = 31 And O = 3 :Then
	1 -> C~D
	0 -> H
	Goto 6
IfEnd
If Ans = 31 And O = 4 :Then
	1 -> H
	1 -> C~D
	7 -> Dim List 11
	Goto 6
IfEnd
Ans = 31 And O = 5 => Goto 4
For 1 -> Z To 100
Next
Goto 9
Lbl 4
ClrText
0 -> I
For 1 -> Y To 5
	For 1 -> Z To 7
		Mat A[Y, Z] -> G
		G = 0 => Locate Z + 1, Y + 2, "■"
		G = 1 => Locate Z + 1, Y + 2, "⊕"
		G = 2 => Locate Z + 1, Y + 2, "○"
		G = 1 Or G = 2 => I + 1 -> I
		If G != 0 And G != 1 And G != 2 :Then
			ClrText
			Locate 1, 3, "Sauvegarde corrompue!"
			For 1 -> Z To 1000
			Next
			Goto 8
		IfEnd
	Next
Next
If C = 0 Or D = 0 :Then
	ClrText
	Locate 1, 3, "Sauvegarde corrompue!"
	For 1 -> Z To 1000
	Next
	Goto 8
IfEnd
F != 0 And F != 1 => 0 -> F
Goto 6
Lbl 6
If O != 5 :Then
	1
	If Ans = 1 :Then
		1 -> F
	Else 
		0 -> F
	IfEnd
	ClrText
IfEnd
Locate 10, 1, "‖&nPr;uissance⁴≡"
Locate 10, 2, "‖≡&peta;ar  &tera;imo≡"
Locate 10, 3, "‖"
Locate 10, 4, "‖ Victoires:"
Locate 10, 5, "‖      J1:"
Locate 10, 6, "‖      J2:"
Locate 10, 7, "‖"
Locate 20, 5, C - 1
Locate 20, 6, D - 1
O -> E
E != 5 => Goto 5
Goto 1
Lbl 5
If F = 0 :Then
	1 -> F
Else 
	0 -> F
IfEnd
{5, 7} -> Dim Mat A
Locate 1, 1, "         "
For 3 -> Z To 7
	Locate 2, Z, "■■■■■■■"
Next
Locate 20, 5, C - 1
Locate 20, 6, D - 1
7 -> A
Lbl 1
5 -> Y
I + 1 -> I
If I = 36 :Then
	Locate 1, 1, "Match nul"
	For 1 -> Z To 1000
	Next
	Do
		GetKey
	LpWhile Ans = 0
	0 -> I
	Goto 5
IfEnd
Lbl 2
If H = 1 And F = 1 :Then
	Prog "-PUIS4IA"
	Prog "-P4WINCK"
	If K > 4 :Then
		D + 1 -> D
		Locate 1, 1, "IA gagne!"
		For 1 -> Z To 1000
		Next
		Do
			GetKey
		LpWhile Ans = 0
		0 -> I
		Goto 5
	IfEnd
	0 -> F
	Goto 1
IfEnd
F = 0 => Locate Y, 1, "⊕"
F = 1 => Locate Y, 1, "○"
Do
	GetKey
LpWhile Ans != 27 And Ans != 38 And Ans != 37 And Ans != 72 And Ans != 62 And Ans != 52 And Ans != 73 And Ans != 31
Locate 2, 1, "       "
Ans = 73 => Goto 8
If Ans = 72 :Then
	Goto 2
IfEnd
If Ans = 62 :Then
	Goto 2
IfEnd
If Ans = 52 :Then
	0 -> I
	Goto 5
IfEnd
Ans = 37 Or Ans = 31 => Goto 3
Ans = 27 => Y + 1 -> Y
Ans = 38 => Y - 1 -> Y
Y = 9 => 2 -> Y
Y = 1 => 8 -> Y
F = 0 => Locate Y, 1, "⊕"
F = 1 => Locate Y, 1, "○"
For 1 -> Z To 100
Next
Goto 2
Lbl 3
Y - 1 -> B
5 -> A
While A > 0
	If Mat A[A, B] = 0 :Then
		F = 0 => 1 -> Mat A[A, B]
		F = 1 => 2 -> Mat A[A, B]
		A + 2 -> W
		F = 0 => Locate Y, W, "⊕"
		F = 1 => Locate Y, W, "○"
		For 1 -> Z To 300
		Next
		Prog "-P4WINCK"
		If K > 4 :Then
			If F = 0 :Then
				C + 1 -> C
			Else 
				D + 1 -> D
			IfEnd
			Locate 1, 1, "J  gagne!"
			Locate 2, 1, F + 1
			For 1 -> Z To 1000
			Next
			Do
				GetKey
			LpWhile Ans = 0
			0 -> I
			Goto 5
		IfEnd
		If F = 0 :Then
			1 -> F
			Goto 1
		Else 
			0 -> F
			Goto 1
		IfEnd
	IfEnd
	A - 1 -> A
WhileEnd
Goto 2
#End of program
#Program name: -P4WINCK
#Password: <no password>
A -> M
B -> N
0 -> L
0 -> K
0 -> Z
F + 1 -> F
If L = 0 :Then
	While Z = 0
		If M > 0 :Then
			If Mat A[M, N] = F :Then
				M - 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
	A -> M
	0 -> Z
	While Z = 0
		If M < 6 :Then
			If Mat A[M, N] = F :Then
				M + 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
IfEnd
K > 4 => Goto 9
1 -> L
0 -> Z
0 -> K
A -> M
If L = 1 :Then
	While Z = 0
		If N > 0 :Then
			If Mat A[M, N] = F :Then
				N - 1 -> N
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
	B -> N
	0 -> Z
	While Z = 0
		If N < 8 :Then
			If Mat A[M, N] = F :Then
				N + 1 -> N
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
IfEnd
K > 4 => Goto 9
2 -> L
0 -> Z
0 -> K
A -> M
B -> N
If L = 2 :Then
	While Z = 0
		If N > 0 And M > 0 :Then
			If Mat A[M, N] = F :Then
				N - 1 -> N
				M - 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
	A -> M
	B -> N
	0 -> Z
	While Z = 0
		If N < 8 And M < 6 :Then
			If Mat A[M, N] = F :Then
				N + 1 -> N
				M + 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
IfEnd
K > 4 => Goto 9
3 -> L
0 -> Z
0 -> K
A -> M
B -> N
If L = 3 :Then
	While Z = 0
		If N < 8 And M > 0 :Then
			If Mat A[M, N] = F :Then
				N + 1 -> N
				M - 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
	A -> M
	B -> N
	0 -> Z
	While Z = 0
		If N > 0 And M < 6 :Then
			If Mat A[M, N] = F :Then
				N - 1 -> N
				M + 1 -> M
				K + 1 -> K
				Z - 1 -> Z
			IfEnd
		IfEnd
		Z + 1 -> Z
	WhileEnd
IfEnd
K > 4 => Goto 9
Lbl 9
F - 1 -> F
#End of program
#Program name: -PUIS4IA
#Password: <no password>
1 -> F
0 -> Y
Locate 1, 1, "Calcul..."
Lbl 1
I < 5 => Goto 8
For 1 -> B To 7
	5 -> A
	While A > 0
		If Mat A[A, B] = 0 :Then
			F = 0 => 1 -> Mat A[A, B]
			F = 1 => 2 -> Mat A[A, B]
			Prog "-P4WINCK"
			If K > 4 :Then
				Goto 9
			Else 
				0 -> Mat A[A, B]
				Goto 4
			IfEnd
		IfEnd
		A - 1 -> A
	WhileEnd
	Lbl 4
Next
0 -> F
Y + 1 -> Y
Y < 2 => Goto 1
1 -> F
5 -> A
0 -> θ
Lbl 3
θ + 1 -> θ
RanInt#(1, 7)
Ans -> B
5 -> A
While A > 0
	If Mat A[A, B] = 0 :Then
		θ > 30 => Goto 9
		If A > 1 :Then
			A - 1 -> A
			0 -> F
			1 -> Mat A[A, B]
			Prog "-P4WINCK"
			0 -> Mat A[A, B]
			1 -> F
			A + 1 -> A
			If K > 4 :Then
				0 -> K
				For 1 -> Z To 7
					Mat A[1, Z] = 0 And Z != B => 1 -> K
				Next
				If K = 1 :Then
					Goto 3
				Else 
					Goto 9
				IfEnd
			Else 
				Goto 9
			IfEnd
		Else 
			Goto 9
		IfEnd
	IfEnd
	A - 1 -> A
WhileEnd
Goto 3
Lbl 2
2 -> Mat A[A, B]
Goto 9
Lbl 8
For 1 -> Z To 500
Next
If I = 1 :Then
	5 -> A
	4 -> B
IfEnd
If I = 2 :Then
	If Mat A[5, 4] != 0 :Then
		RanInt#(1, 2)
		Ans = 1 => 3 -> B
		Ans = 2 => 5 -> B
		5 -> A
	Else 
		5 -> A
		4 -> B
	IfEnd
IfEnd
If I = 3 :Then
	RanInt#(1, 2)
	Ans = 1 => 3 -> B
	Ans = 2 => 5 -> B
	5 -> A
	Mat A[5, 3] != 0 => 5 -> B
	Mat A[5, 5] != 0 => 3 -> B
IfEnd
If I = 4 :Then
	If Mat A[4, 4] != 0 :Then
		3 -> A
	Else 
		4 -> A
	IfEnd
	4 -> B
IfEnd
Lbl 9
Locate B + 1, A + 2, "○"
2 -> Mat A[A, B]
Locate 1, 1, "         "
#End of program
